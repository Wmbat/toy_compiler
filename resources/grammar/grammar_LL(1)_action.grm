<AParams> ::= <Expr> <AParamsTail> 
<AParams> ::= EPSILON (Epsilon) 

<AParamsTail> ::= ',' <Expr> <AParamsTail> 
<AParamsTail> ::= EPSILON  (Epsilon)

<AddOp> ::= '+' 
<AddOp> ::= '-' 
<AddOp> ::= 'or' 

<ArithExpr> ::= <Term> <ArithExprTail> 

<ArithExprTail> ::= <AddOp> (ActionIdDecl) <Term> (ActionAddOp) <ArithExprTail> 
<ArithExprTail> ::= EPSILON 

<ArraySizeRept> ::= '[' <IntNum> ']' <ArraySizeRept> 
<ArraySizeRept> ::= EPSILON 

<AssignOp> ::= 'assign' 

<AssignStatTail> ::= <AssignOp> <Expr> 

<ClassDecl> ::= 'class' 'id' <Inherit> '{' <ClassDeclBody> '}' ';' <ClassDecl> 
<ClassDecl> ::= EPSILON 

<ClassDeclBody> ::= <Visibility> <MemberDecl> <ClassDeclBody> 
<ClassDeclBody> ::= EPSILON 

<ClassMethod> ::= 'sr' 'id' 
<ClassMethod> ::= EPSILON 

<Expr> ::= <ArithExpr> <ExprTail> 

<ExprTail> ::= <RelOp> (ActionIdDecl) <ArithExpr> (ActionRelOp)
<ExprTail> ::= EPSILON 

<FParams> ::= <Type> 'id' <ArraySizeRept> <FParamsTail> 
<FParams> ::= EPSILON 

<FParamsTail> ::= ',' <Type> 'id' <ArraySizeRept> <FParamsTail> 
<FParamsTail> ::= EPSILON 

<Factor> ::= <FuncOrVar> (FuncOrVarExpr)
<Factor> ::= 'intnum' (IntExpr)
<Factor> ::= 'floatnum' (FloatExpr)
<Factor> ::= 'stringlit' (StrExpr)
<Factor> ::= '(' (location) <Expr> ')' (PriorityExpr)
<Factor> ::= 'not' (id) <Factor> (NotExpr)
<Factor> ::= <Sign> (sign) <Factor> (SignExpr)
<Factor> ::= 'qm' (location) '[' <Expr> ':' <Expr> ':' <Expr> ']' (QmExpr)

<FuncBody> ::= '{' <MethodBodyVar> <StatementList> (CompoundStmt)'}' 

<FuncDecl> ::= 'func' 'id' '(' <FParams> ')' ':' <FuncDeclTail> ';' 

<FuncDeclTail> ::= <Type> 
<FuncDeclTail> ::= 'void' 

<FuncDef> ::= <Function> <FuncDef> 
<FuncDef> ::= EPSILON 

<FuncHead> ::= 'func' 'id' <ClassMethod> '(' <FParams> ')' ':' <FuncDeclTail> 

<FuncOrAssignStat> ::= 'id' <FuncOrAssignStatIdnest> 

<FuncOrAssignStatIdnest> ::= <IndiceRep> <FuncOrAssignStatIdnestVarTail> 
<FuncOrAssignStatIdnest> ::= '(' <AParams> ')' <FuncOrAssignStatIdnestFuncTail> 

<FuncOrAssignStatIdnestFuncTail> ::= '.' 'id' <FuncStatTail> 
<FuncOrAssignStatIdnestFuncTail> ::= EPSILON 

<FuncOrAssignStatIdnestVarTail> ::= '.' 'id' <FuncOrAssignStatIdnest> 
<FuncOrAssignStatIdnestVarTail> ::= <AssignStatTail> 

<FuncOrVar> ::= 'id' (IdDecl) <FuncOrVarIdnest> 

<FuncOrVarIdnest> ::= <IndiceRep> (CompoundArrayIndexAccessDecl) (VarExpr) <FuncOrVarIdnestTail> 
<FuncOrVarIdnest> ::= '(' <AParams> ')' (CompoundParameterExprDecl) (FuncExpr) <FuncOrVarIdnestTail> 

<FuncOrVarIdnestTail> ::= '.' 'id' (IdDecl) <FuncOrVarIdnest> 
<FuncOrVarIdnestTail> ::= EPSILON 

<FuncStatTail> ::= <IndiceRep> '.' 'id' <FuncStatTail> 
<FuncStatTail> ::= '(' <AParams> ')' <FuncStatTailIdnest> 

<FuncStatTailIdnest> ::= '.' 'id' <FuncStatTail> 
<FuncStatTailIdnest> ::= EPSILON 

<Function> ::= <FuncHead> <FuncBody> 

<IndiceRep> ::= '[' <Expr> ']' (ArrayIndexAccessDecl) <IndiceRep> 
<IndiceRep> ::= EPSILON (Epsilon)

<Inherit> ::= 'inherits' 'id' <NestedId> 
<Inherit> ::= EPSILON 

<NestedId> ::= ',' 'id' <NestedId> 
<NestedId> ::= EPSILON 

<IntNum> ::= 'intnum' 
<IntNum> ::= EPSILON 

<MemberDecl> ::= <FuncDecl> 
<MemberDecl> ::= <VarDecl> 

<MethodBodyVar> ::= 'var' '{' <VarDeclRep> '}' 
<MethodBodyVar> ::= EPSILON 

<MultOp> ::= '*' 
<MultOp> ::= '/' 
<MultOp> ::= 'and' 

<Prog> ::= <ClassDecl> <FuncDef> 'main' <FuncBody> 

<RelOp> ::= 'eq' 
<RelOp> ::= 'neq' 
<RelOp> ::= 'lt' 
<RelOp> ::= 'gt' 
<RelOp> ::= 'leq' 
<RelOp> ::= 'geq' 

<START> ::= <Prog> 

<Sign> ::= '+' 
<Sign> ::= '-' 

<StatBlock> ::= '{' <StatementList> (CompoundStmt) '}' 
<StatBlock> ::= <Statement>
<StatBlock> ::= EPSILON (Epsilon)

<Statement> ::= <FuncOrAssignStat> ';' 
<Statement> ::= 'if' (LocationDecl) '(' <Expr> ')' 'then' <StatBlock> (StmtBlockDecl) 'else' <StatBlock> (StmtBlockDecl) (IfStmt) ';' 
<Statement> ::= 'while' (LocationDecl) '(' <Expr> ')' <StatBlock> (StmtBlockDecl) (WhileStmt) ';' 
<Statement> ::= 'read' '(' <Variable> ')' ';' 
<Statement> ::= 'write' '(' <Expr> ')' (WriteStmt) ';' 
<Statement> ::= 'return' '(' <Expr> ')' (ReturnStmt) ';' 
<Statement> ::= 'break' (BreakStmt) ';' 
<Statement> ::= 'continue' (ContinueStmt) ';' 

<StatementList> ::= <Statement> <StatementList> 
<StatementList> ::= EPSILON 

<Term> ::= <Factor> <TermTail> 

<TermTail> ::= <MultOp> (ActionIdDecl) <Factor> (ActionMultOp) <TermTail> 
<TermTail> ::= EPSILON

<Type> ::= 'integer' 
<Type> ::= 'float' 
<Type> ::= 'string' 
<Type> ::= 'id' 

<VarDecl> ::= <Type> 'id' <ArraySizeRept> ';' 

<VarDeclRep> ::= <VarDecl> <VarDeclRep> 
<VarDeclRep> ::= EPSILON 

<Variable> ::= 'id' <VariableIdnest> 

<VariableIdnest> ::= <IndiceRep> <VariableIdnestTail> 

<VariableIdnestTail> ::= '.' 'id' <VariableIdnest> 
<VariableIdnestTail> ::= EPSILON 

<Visibility> ::= 'public' 
<Visibility> ::= 'private' 
<Visibility> ::= EPSILON 


